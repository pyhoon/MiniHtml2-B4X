B4J=true
Group=Handlers
ModulesStructureVersion=1
Type=Class
Version=10.2
@EndOfDesignText@
'Handler class
Sub Class_Globals
	Private Response As ServletResponse
	Private Method As String
End Sub

Public Sub Initialize

End Sub

Sub Handle (req As ServletRequest, resp As ServletResponse)
	Response = resp
	Method = req.Method.ToUpperCase
	Dim path As String = req.RequestURI
	If path = "/" Then
		If Method = "GET" Then
			ShowIndexPage
			Return
		End If
	Else If path = "/modal" Then
		If Method = "POST" Then
			Response.Write(GenerateModal)
			Return
		End If
		Response.SendError(405, "<h1>405 Method Not Allowed</h1>")
		Return
	End If
	Response.SendError(404, "<h1>404 Page Not Found</h1>")
End Sub

Private Sub ShowIndexPage
	Dim doc As Document
	doc.Initialize
	doc.AppendDocType
	doc.Append(GenerateIndex)
	Response.Write(doc.ToString)
End Sub

Sub CreateTag (Name As String) As MiniHtml
	Dim MiniHtml2 As MiniHtml
	MiniHtml2.Initialize(Name)
	Return MiniHtml2
End Sub

'Add metas for UTF8 charset and mobile viewport
'<code>SetResponsive(head1)</code>
Public Sub SetResponsive (Head1 As MiniHtml)
	Head1.add(CreateTag("meta").attr("charset", "UTF-8"))
	Head1.add(CreateTag("meta").attr2(CreateMap("name": "viewport", "content": "width=device-width, initial-scale=1")))
End Sub

Sub GenerateIndex As String
	Dim html1 As MiniHtml = CreateTag("html").attr("lang", "en")
	html1.comment(" generated by MiniHTML2 ")
	Dim head1 As MiniHtml = CreateTag("head").up(html1)
	SetResponsive(head1)
	head1.add(CreateTag("title").text("MiniHtml2 Demo"))
	head1.cdn2("style", "https://cdn.jsdelivr.net/npm/bootstrap@5.3.8/dist/css/bootstrap.min.css", "", "")
	head1.cdn2("script", "https://cdn.jsdelivr.net/npm/bootstrap@5.3.8/dist/js/bootstrap.bundle.min.js", "", "")
	head1.cdn2("script", "https://cdn.jsdelivr.net/npm/htmx.org@2.0.8/dist/htmx.min.js", "", "")

	Dim body1 As MiniHtml = CreateTag("body").up(html1)
	Dim div1 As MiniHtml = CreateTag("div").up(body1)
	div1.cls("d-flex align-items-center")
	div1.sty("height: 100vh")
	Dim div2 As MiniHtml = CreateTag("div").up(div1)
	div2.sty("margin: auto")

	Dim div3 As MiniHtml = CreateTag("div").up(div2)
	div3.attr("id", "modals-here")
	div3.cls("modal modal-blur fade")
	div3.sty("display: none")
	div3.attr("aria-hidden", "false")
	div3.attr("tabindex", "-1")
	
	Dim div4 As MiniHtml = CreateTag("div").up(div3)
	div4.cls("modal-dialog modal-lg modal-dialog-centered")
	div4.attr("role", "document")
	CreateTag("div").up(div4).cls("modal-content")

	Dim button1 As MiniHtml = CreateTag("button").up(div2)
	button1.attr("hx-post", "/modal")
	button1.attr("hx-target", "#modals-here")
	button1.attr("hx-trigger", "click")
	button1.attr("data-bs-toggle", "modal")
	button1.attr("data-bs-target", "#modals-here")
	button1.cls("btn btn-primary text-uppercase")
	button1.text("Open Modal")
	button1.uniline
	Return html1.Build
End Sub

Sub GenerateModal As String
	Dim div4 As MiniHtml = CreateTag("div")
	div4.cls("modal-dialog modal-lg modal-dialog-centered")
	Dim div5 As MiniHtml = CreateTag("div").up(div4)
	div5.cls("modal-content")
	Dim div6 As MiniHtml = CreateTag("div").up(div5)
	div6.cls("modal-header")
	Dim h5 As MiniHtml = CreateTag("h5").up(div6)
	h5.cls("modal-title")
	h5.text("Modal title")
	Dim div6 As MiniHtml = CreateTag("div").up(div5)
	div6.cls("modal-body")
	Dim p1 As MiniHtml = CreateTag("p").up(div6)
	p1.text("Modal body text goes here.")
	p1.uniline
	Dim div6 As MiniHtml = CreateTag("div").up(div5)
	div6.cls("modal-footer")
	Dim button1 As MiniHtml = CreateTag("button").up(div6)
	button1.attr("type", "button")
	button1.cls("btn btn-secondary text-uppercase")
	button1.attr("data-bs-dismiss", "modal")
	button1.text("Close")
	button1.uniline
	Return div4.build
End Sub